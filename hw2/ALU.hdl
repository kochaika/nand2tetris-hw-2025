// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //set values, make x and y equal 0, and not x and y
    Mux16(a=x, sel=zx, out=zeroX);
    Mux16(a=y, sel=zy, out=zeroY);
    
    Not16(in=zeroX, out=negX);
    Not16(in=zeroY, out=negY);

    Mux16(a=zeroX, b=negX, sel=nx, out=tempX);
    Mux16(a=zeroY, b=negY, sel=ny, out=tempY);


    //f selection result
    Add16(a=tempX, b=tempY, out=XplusY);
    And16(a=tempX, b=tempY, out=XandY);
    Mux16(a=XandY, b=XplusY, sel=f, out=tempOut);

    //negate output, no selector
    Not16(in=tempOut, out=negOut);
    Mux16(a=tempOut, b=negOut, sel=no, out = out, out[15]=msb, out[0..7]=lowOut, out[8..15]=highOut);
  
    //zr check
    Or8Way(in=lowOut, out = orLow);
    Or8Way(in=highOut, out = orHigh);
    Or(a = orLow, b = orHigh, out = orAll);
    Not(in = orAll, out = zr);

    //ng, msb
    And(a = msb, b = true, out = ng);
}