// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // Process x input
    Mux16(a=x, b=false, sel=zx, out=xclean);
    Not16(in=xclean, out=xflipped);
    Mux16(a=xclean, b=xflipped, sel=nx, out=xprocessed);

    // Process y input
    Mux16(a=y, b=false, sel=zy, out=yclean);
    Not16(in=yclean, out=yflipped);
    Mux16(a=yclean, b=yflipped, sel=ny, out=yprocessed);

    // Compute ADD and AND results
    Add16(a=xprocessed, b=yprocessed, out=sumresult);
    And16(a=xprocessed, b=yprocessed, out=andresult);

    // Select final operation result
    Mux16(a=andresult, b=sumresult, sel=f, out=aluoutput);

    // Negate if needed
    Not16(in=aluoutput, out=negatedoutput);
    Mux16(a=aluoutput, b=negatedoutput, sel=no, out=out, out[0..7] = zerochecklow, out[8..15] = zerocheckhigh, out[15] = ng);

    // Compute zr (zero flag)
    Or8Way(in=zerochecklow, out=zrlow);
    Or8Way(in=zerocheckhigh, out=zrhigh);
    Or(a=zrlow, b=zrhigh, out=notzero);
    Not(in=notzero, out=zr);
}