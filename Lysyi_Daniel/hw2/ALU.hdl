// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Not16(in=x, out=px);
    Inc16(in=px, out=negx);
    Add16(a=x, b=negx, out=zero);
    Mux16(a=x, b=zero, sel=zx, out=xzx);
    Not16(in=xzx, out=negatedxzx);
    Mux16(a= xzx, b= negatedxzx, sel=nx, out=xzxnx);

    Mux16(a=y, b=zero, sel=zy, out=yzy);
    Not16(in=yzy, out=negatedyzy);
    Mux16(a= yzy, b= negatedyzy, sel=ny, out=yzyny);

    And16(a=xzxnx, b=yzyny, out=res1);
    Add16(a=xzxnx, b=yzyny, out=res2);
    Mux16(a=res1, b=res2, sel=f, out=almostanswer);
    Not16(in=almostanswer, out =negatedalmostanswer);
    Mux16(a=almostanswer, b=negatedalmostanswer, sel=no, out=out, out[0..7]=left, out[8..15]=right, out[15]=ng);
    Or8Way(in=left, out=lsum);
    Or8Way(in=right, out=rsum);
    Or(a=lsum, b=rsum, out=z);
    Not(in=z, out=zr);

    


}