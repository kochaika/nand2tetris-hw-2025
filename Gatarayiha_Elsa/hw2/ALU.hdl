// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //implementing 'zx' and 'nx' for x0 and x1
    Mux16(a=x , b=false , sel=zx , out=xzero );
    Not16(in=xzero , out=nxzero );
    Mux16(a=xzero , b=nxzero , sel=nx , out=xval );
    //implementing 'zy' and 'ny'for y0 and y1
    Mux16(a=y , b=false , sel=zy , out=yzero );
    Not16(in=yzero , out=nyzero );
    Mux16(a=yzero , b=nyzero , sel=ny , out=yval );
    

    //computing 'x+y', 'x&y' and f
    And16(a=xval , b=yval , out=fand );
    Add16(a=xval , b=yval , out=fadd );
    Mux16(a=fand , b=fadd , sel=f , out=choosef );

    //handling negative out case and implementing 'ng' and
    //  setting the output flag 'zr'
    Not16(in=choosef , out=nf );
    Mux16(a=choosef , b=nf , sel=no , out=out,
    out=outflag,
    out[0..7]=outflag1,
    out[8..15]=outflag2,
    out[15]=ng);
 
    //zr flag
    Or8Way(in=outflag1, out=out1);
    Or8Way(in=outflag2, out=out2);
    Or(a=out1, b=out2, out=nzr);
    Not(in=nzr, out=zr);
}