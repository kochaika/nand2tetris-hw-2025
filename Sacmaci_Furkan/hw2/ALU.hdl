// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //x
    Mux16(a= x, sel= zx, out= x1);
    Not16(in= x1, out= minusx);
    Mux16(a= x1, b= minusx, sel= nx, out= x2);

    //y
    Mux16(a= y, sel= zy, out= y1);
    Not16(in= y1, out= minusy);
    Mux16(a= y1, b= minusy, sel= ny, out= y2);

    //x and y OR x + y
    Add16(a = x2, b = y2, out = xaddy);
    And16(a= x2, b= y2, out= xandy);
    Mux16(a= xandy, b= xaddy, sel= f, out= k);

    //negate output OR not
    Not16(in= k, out= minusk);
    
    //Mux16 for zr AND ng
    Mux16(a= k, b= minusk, sel= no, out= out, out[15]=leftmost, out[8..15]=righteight, out[0..7]=lefteight );
    
    //zr
    Or8Way(in= lefteight, out= p1);
    Or8Way(in= righteight, out= p2);
    Or(a= p1, b= p2, out= p3);
    Not(in= p3, out= zr);

    //ng
    And(a=leftmost, b=true, out=ng);
}